#+title: doom emacs config of lijigang

* èƒŒæ™¯
#+begin_quote
é…ç½®åŸåˆ™ï¼š *å¦‚æ— å¿…è¦, å‹¿å¢å®ä½“*
#+end_quote

é…ç½®çš„æ¯è¡Œä»£ç äº‰å–åšåˆ° =å‡æ— å¯å‡= çš„çŠ¶æ€, å¢åŠ çš„æ¯ä¸€è¡Œéƒ½æœ‰å…¶åŸå› ã€‚

* æˆªå›¾
#+attr_org: :width 600px
[[file:images/demo1.png]]

#+attr_org: :width 600px
[[file:images/demo2.png]]

#+attr_org: :width 600px
[[file:images/demo3.png]]

#+attr_org: :width 600px
[[file:images/demo4.png]]

* å®‰è£…
1. å®‰è£… Doom
   #+begin_src shell
   git clone --depth 1 https://github.com/hlissner/doom-emacs ~/.emacs.d

   ~/.emacs.d/bin/doom install
   #+end_src
2. ä¸‹è½½æœ¬é…ç½®æ–‡ä»¶åˆ°æœ¬åœ°
   #+begin_src shell
    git clone git@github.com:lijigang/emacs.d.git
   #+end_src
3. Tangle ä»£ç ç”Ÿæˆé…ç½®
  #+begin_src
   ;; emacs readme.org
   M-x: org-babel-tangle
  #+end_src

* é…ç½®
** å¼€å…³: Doom è‡ªå¸¦çš„æ¨¡å—
:PROPERTIES:
:header-args: :tangle "~/.doom.d/init.el"
:header-args: :mkdirp yes
:END:
#+begin_src emacs-lisp :tangle "~/.doom.d/init.el"
;;; init.el -*- lexical-binding: t; -*-

(doom!
       :completion
       (ivy
        +icons
        +prescient)       ; a search engine for love and life

       :ui
       doom               ; what makes DOOM look the way it does
       modeline
       hl-todo            ; highlight TODO/FIXME/NOTE/DEPRECATED/HACK/REVIEW
       (ligatures +extra)
       ophints            ; highlight the region an operation acts on
       (popup
        +all
        +defaults)        ; tame sudden yet inevitable temporary windows
       unicode
       zen                ; distraction-free coding or writing

       :editor
       (evil +everywhere) ; come to the dark side, we have cookies
       file-templates     ; auto-snippets for empty files
       fold               ; (nigh) universal code folding
       (format +onsave)   ; automated prettiness
       snippets           ; my elves. They type so I don't have to
       word-wrap

       :emacs
       ;; electric           ; smarter, keyword-based electric-indent
       (ibuffer +icons)   ; interactive buffer management
       undo               ; persistent, smarter undo for your inevitable mistakes

       :checkers
       syntax             ; tasing you for every semicolon you forget

       :tools
       (eval +overlay)    ; run code, run (also, repls)
       lookup             ; navigate your code and its documentation
       magit              ; a git porcelain for Emacs
       pdf                ; pdf enhancements
       rgb                ; creating color strings

       :os
       (:if IS-MAC macos) ; improve compatibility with macOS

       :lang
       emacs-lisp         ; drown in parentheses
       latex              ; writing papers in Emacs has never been so fun
       markdown

       (org               ; organize your plain life in plain text
        +roam2
        +dragndrop
        +gnuplot
        +hugo
        +pandoc
        +pretty
        +present)

       plantuml           ; diagrams for confusing people more
       rest               ; for use restclient
       sh                 ; she sells {ba,z,fi}sh shells on the C xor
       yaml               ; JSON, but readable

       :email
       (mu4e +org)

       :app
       ;; calendar
       (rss +org)         ; emacs as an RSS reader

       :config
       (default +bindings +smartparens))
#+end_src
** åŠ è£…: é¢å¤–éœ€è¦çš„åŠŸèƒ½åŒ…
:PROPERTIES:
:header-args: :tangle "~/.doom.d/packages.el"
:header-args: :mkdirp yes
:END:
#+begin_src emacs-lisp :tangle "~/.doom.d/packages.el"
;; -*- no-byte-compile: t; -*-
;;; $DOOMDIR/packages.el

;; å¿«é€Ÿè·³è½¬åˆ°ä»»æ„ä½ç½®, é€šè¿‡æ±‰å­—æ‹¼éŸ³çš„æ–¹å¼
(package! ace-pinyin
  :recipe (:host github :repo "cute-jumper/ace-pinyin"))

;; å¥½ç”¨çš„ç¿»è¯‘åŒ…
(package! fanyi)

;; é«˜äº®å½“å‰æ®µè½, Dim å…¶å®ƒåŒºåŸŸ, ä¿æŒFocus!
(package! focus)

(package! gptel)

(package! imenu-list)

(package! olivetti)

;; é¼ æ ‡æ”¾åˆ°åŠ ç²—å­—ç¬¦ä¸Š, å¯ç¼–è¾‘ä¿®é¥°ç¬¦, ç¦»å¼€å³æ˜¾ç¤ºåŠ ç²—åçš„æ•ˆæœ
(package! org-appear
  :recipe (:host github :repo "awth13/org-appear"))

;; åœ¨Orgmode æ–‡ä»¶ä¸­æ’å…¥å›¾ç‰‡
(package! org-download)

(package! org-imenu
  :recipe (:host github :repo "rougier/org-imenu"))

;; ä¸€ä¸ªå¿«é€ŸæŸ¥è¯¢org æ–‡ä»¶ç›¸å…³å†…å®¹çš„ query language
(package! org-ql)

(package! org-roam-ui)

;; æ–‡ä»¶é—´å¼•ç”¨çš„æ’ä»¶
(package! org-transclusion)

;; ä¸­è‹±æ–‡å­—ç¬¦ä¹‹é—´è‡ªåŠ¨æ’å…¥ç©ºæ ¼, å¢åŠ å¯é˜…è¯»æ€§
(package! pangu-spacing)

;; æ¯ä¸ªæ ‡è¯†ç¬¦æ˜¾ç¤ºä¸€ä¸ªé¢œè‰², èŠ±é‡Œèƒ¡å“¨çš„å¼€å§‹
(package! rainbow-identifiers)

;; åœ¨Emacs ä¸­ä½¿ç”¨rime, å‡å°‘åˆ‡æ¢ä¸­è‹±æ–‡çŠ¶æ€
(package! rime)

(package! telega
  :recipe (:host github :repo "zevlg/telega.el"))

;; åœ¨orgmode æ–‡ä»¶ä¸­è¿›è¡Œè®¡ç®—å™¨å¼çš„æ“ä½œ
(package! literate-calc-mode)

;; å®Œç¾è§£å†³ä¸­è‹±æ–‡å­—ç¬¦åœ¨è¡¨æ ¼ä¸­å¯¹é½çš„é—®é¢˜
(package! valign)
#+end_src
** é…ç½®: ä½ æƒ³è¦çš„æ•ˆæœ
:PROPERTIES:
:header-args: :tangle "~/.doom.d/config.el"
:header-args: :mkdirp yes
:END:
*** é€šç”¨é…ç½®
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-

;; é€šè¿‡Emacs-china çš„ä»“åº“æ¥å®‰è£…åŒ…, æå‡å®‰è£…é€Ÿåº¦
(setq package-archives '(("gnu" . "http://elpa.emacs-china.org/gnu/")
                         ("melpa" . "http://elpa.emacs-china.org/melpa/")))

;; ä¸ªäººä¿¡æ¯
(setq user-full-name "lijigang"
      user-mail-address "i@lijigang.com")

;; é€šè¿‡iCloud è‡ªåŠ¨åŒæ­¥Documents ç›®å½•, å¤šå°ç”µè„‘å¯ä»¥æ— ç¼è¿ç§»ä½¿ç”¨
(setq org-directory "~/Documents/emacs/org/")

(setq confirm-kill-emacs nil ; å…³é—­ emacs æ—¶æ— éœ€é¢å¤–ç¡®è®¤
      system-time-locale "C" ; è®¾ç½®ç³»ç»Ÿæ—¶é—´æ˜¾ç¤ºæ–¹å¼
      pop-up-windows nil     ; no pop-up window
      scroll-margin 2        ; It's nice to maintain a little margin
      widget-image-enable nil
      visible-bell nil
      ring-bell-function 'ignore)

(prefer-coding-system 'utf-8)
(set-default-coding-systems 'utf-8)
(setq default-buffer-file-coding-system 'utf-8)

;; å…¨å±€æ‰“å¼€visual line
(global-visual-line-mode)

(setq word-wrap-by-category t)

(add-hook 'org-mode-hook 'adaptive-wrap-prefix-mode)

;; æŸ¥å¸®åŠ©æ–‡æ¡£æ—¶, æ˜¾ç¤ºä½¿ç”¨Demos
(advice-add 'helpful-update :after #'elisp-demos-advice-helpful-update)

;; æ‰“å¼€æ–‡ä»¶æ—¶, å…‰æ ‡è‡ªåŠ¨å®šä½åˆ°ä¸Šæ¬¡åœç•™çš„ä½ç½®
(save-place-mode 1)

(global-auto-revert-mode)

(setq display-line-numbers-type nil)

;; å…³é—­Doom rss åˆ‡å‰²å›¾ç‰‡çš„åŠŸèƒ½, å±•ç¤ºå®Œæ•´å›¾ç‰‡å§
(setq +rss-enable-sliced-images nil)

;; æ³¨æ„Doom æœ‰ Bug : ~/.emacs.d/modules/editor/word-wrap/autoload.el
;; (symbol-value +word-wrap--major-mode-indent-var)
;; æ‰‹åŠ¨æ”¹ä¸º
;; (symbol-value '+word-wrap--major-mode-indent-var)
;; åŒæ—¶è®¾ç½®å˜é‡å€¼
(setq +word-wrap--major-mode-indent-var 1)

(blink-cursor-mode 0)
(show-paren-mode t)
(fringe-mode '(0 . 0)) ;; No fringe

#+end_src
*** é…ç½® Frame
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;; æŒ‡å®šå¯åŠ¨æ—¶çš„çª—å£ä½ç½®å’Œå¤§å°
(setq initial-frame-alist '((top . 45)
                             (left . 1200)
                             (width . 100)
                             (height . 45)))

(setq frame-title-format "ç”Ÿäº§çŸ¥è¯†")
(menu-bar-mode -1) ;; minimal chrome
(tool-bar-mode -1) ;; no toolbar
(scroll-bar-mode -1) ;; disable scroll bars

(setq initial-major-mode 'org-mode) ;; org!
(setq initial-scratch-message nil)
#+end_src
*** é…ç½® Window
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

;; æ–°æ‰“å¼€çª—å£æ—¶, æç¤ºè¦æ‰“å¼€å“ªä¸ªBuffer
(setq evil-vsplit-window-right t
      evil-split-window-below t)

(defadvice! prompt-for-buffer (&rest _)
  :after '(evil-window-split evil-window-vsplit)
  (+ivy/switch-buffer))

(setq +ivy-buffer-preview t)

(setq-default x-stretch-cursor t ;; Stretch cursor to the glyph width
              line-spacing 0.2
              fill-column 80
              x-underline-at-descent-line t)

#+end_src
*** é…ç½®é¼ æ ‡æ»šåŠ¨
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

;; Smooth mouse scrolling
(setq mouse-wheel-scroll-amount '(2 ((shift) . 1))  ; scroll two lines at a time
      mouse-wheel-progressive-speed nil             ; don't accelerate scrolling
      mouse-wheel-follow-mouse t                    ; scroll window under mouse
      scroll-step 1)

#+end_src
*** é…ç½®ä¸»é¢˜
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;; for night
(setq doom-theme 'doom-monokai-spectrum)
;; for day
;; (setq doom-theme 'doom-flatwhite)
#+end_src
*** é…ç½®å­—ä½“
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

;; Doom è‡ªå¸¦çš„å­—ä½“å˜é‡é…ç½®

;; Plan A: ä¸­æ–‡è‹¹æ–¹, è‹±æ–‡Roboto Mono
;; (setq doom-font (font-spec :family "Roboto Mono" :size 22)
;;       doom-serif-font doom-font
;;       doom-symbol-font (font-spec :family "PingFang SC")
;;       doom-variable-pitch-font (font-spec :family "PingFang SC" :weight 'extra-bold))

;; Plan B: ä¸­æ–‡è‹¹æ–¹, è‹±æ–‡Zpix åƒç´ é£æ ¼
(setq doom-font (font-spec :family "Zpix" :size 22)
      doom-serif-font doom-font
      doom-symbol-font (font-spec :family "PingFang SC")
      doom-variable-pitch-font (font-spec :family "PingFang SC" :weight 'extra-bold))

;; å¦‚æœä¸æŠŠè¿™ç©æ„è®¾ç½®ä¸º nil, ä¼šé»˜è®¤å»ç”¨ fontset-default æ¥å±•ç¤º, é…ç½®æ— æ•ˆ
(setq use-default-font-for-symbols nil)

;; Doom çš„å­—ä½“åŠ è½½é¡ºåºé—®é¢˜, å¦‚æœä¸è®¾å®šè¿™ä¸ª hook, é…ç½®ä¼šè¢«è¦†ç›–å¤±æ•ˆ
(add-hook! 'after-setting-font-hook
  (set-fontset-font t 'latin (font-spec :family "Roboto Mono"))
  (set-fontset-font t 'symbol (font-spec :family "Symbola"))
  (set-fontset-font t 'mathematical (font-spec :family "Symbola"))
  (set-fontset-font t 'emoji (font-spec :family "Symbola")))
#+end_src
*** é…ç½® LaTeX
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;; Macbook å®‰è£…è·¯å¾„
(setenv "PATH" (concat (getenv "PATH") ":/usr/local/texlive/2022/bin/universal-darwin"))
(setq exec-path (append exec-path '("/usr/local/texlive/2022/bin/universal-darwin")))

;; å®¶é‡ŒiMac å®‰è£…è·¯å¾„
;; (setenv "PATH" (concat (getenv "PATH") ":/usr/local/texlive/2019/bin/x86_64-darwin/"))
;; (setq exec-path (append exec-path '("/usr/local/texlive/2019/bin/x86_64-darwin/")))

(setq org-highlight-latex-and-related '(native script entities))

(pdf-loader-install)

(setq Tex-command-default "XeLaTeX")
(setq org-latex-pdf-process
      '(
        "xelatex -interaction nonstopmode -output-directory %o %f"
        "xelatex -interaction nonstopmode -output-directory %o %f"
        "xelatex -interaction nonstopmode -output-directory %o %f"
        "rm -fr %b.out %b.log %b.tex auto"))

#+end_src

#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(with-eval-after-load 'ox-latex
  (add-to-list 'org-latex-classes
               '("ctexart" "\\documentclass[11pt,titlepage]{ctexart}

% Document title
\\usepackage{titling}

% Page Margins: important
% https://ctan.math.illinois.edu/macros/latex/contrib/geometry/geometry.pdf
% \\usepackage[scale=0.8,centering]{geometry}
\\usepackage{geometry}
\\geometry{
    a4paper,% 210 * 297mm
    hcentering,% å°†hmarginratioè®¾ä¸º1:1ï¼Œå³left=right
    left=28mm,% æ³¨æ„left=right
    top=37.00mm,% Word æ¨¡æ¿é¡µçœ‰é¡¶ç«¯è·ç¦»20mm
    width=156mm,
    height=225mm,
    }

% Page head and foot
% lhead/chead/rhead
% lfoot/cfoot/rfoot
\\usepackage{lastpage}

\\usepackage{fancyhdr}
\\pagestyle{fancy}
\\chead{\\textsc{\\title}}
\\rhead{\\textit{Last modified: \\today}}
\\rfoot{}
\\cfoot{\\color{gray} \\textsc{\\thepage~/~\\pageref*{LastPage}}}
\\lfoot{}
\\renewcommand\\headrulewidth{0.6pt}
\\renewcommand\\footrulewidth{0.6pt}

\\usepackage[most]{tcolorbox}
\\usepackage[colorinlistoftodos]{todonotes}
\\usepackage{tikz-bagua}

%% Highlighted remarks/notes
% Highlighted remark/note with and without title
\\newenvironment{Highlight}[1]
{
        \\ifthenelse{\\equal{#1}{}}{
                \\begin{tcolorbox}[breakable, enhanced, colback=yellow!15!white,colframe=yellow!20!black]
                \\setlength\\parskip{0.2cm}
        }
        {
                \\begin{tcolorbox}[breakable, enhanced, colback=yellow!15!white,colframe=yellow!20!black, fonttitle=\\bfseries, title=#1]
                \\setlength\\parskip{0.2cm}
        }
}
{
        \\end{tcolorbox}
}
\\newtcolorbox{tip}{colback=blue!5!white,colframe=blue!75!black}
\\newtcolorbox{tipt}[1]{colback=blue!5!white,colframe=blue!75!black,fonttitle=\\bfseries,title=#1}

% Format of section and subsection headers
% [rm sf tt bf up it sl sc]
% Select the corresponding family/series/shape. Default is bf.
\\usepackage{titlesec}

% for use notin math symbol
\\usepackage{unicode-math}

% ä½¿ç”¨UTF-8ç¼–ç è¾“å…¥æ–‡å­—
\\usepackage[utf8]{inputenc}
\\usepackage[T1]{fontenc}

% Hyperlinks and bookmarks
\\usepackage{hyperref}
\\hypersetup{colorlinks=true,linkcolor=blue}

% xcolor is more powerful than color
% \\color{red!70}  %70 percent red color
% \\textcolor{red}
% \\colorbox{gray}
\\usepackage[rgb]{xcolor}
% colortble is for org-table
% \\rowclor{gray}
\\usepackage{colortbl}

% Include graphics
\\usepackage{graphicx}

\\usepackage{longtable}
\\usepackage{float}
\\usepackage{wrapfig}

% List items
\\usepackage{enumerate}
%% \\usepackage{enumitem}

% Line spread
\\usepackage{parskip}"
                 ("\\section{%s}" . "\\section*{%s}")
                 ("\\subsection{%s}" . "\\subsection*{%s}")
                 ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                 ("\\paragraph{%s}" . "\\paragraph*{%s}")
                 ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))
  (setq org-latex-default-class "ctexart")
  (setq org-latex-compiler "xelatex"))

#+end_src
*** é…ç½® =OrgMode=
**** å¸¸ç”¨å˜é‡
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(after! org
  (setq org-archive-location (concat org-directory "roam/archive.org::")
        org-log-done t
        org-use-property-inheritance t
        org-confirm-babel-evaluate nil
        org-list-allow-alphabetical t
        org-export-with-sub-superscripts nil
        org-export-headline-levels 5
        org-export-use-babel t
        org-use-speed-commands t
        org-return-follows-link t
        org-hide-emphasis-markers t
        org-special-ctrl-a/e t
        org-special-ctrl-k t
        org-src-preserve-indentation nil
        org-src-tab-acts-natively t
        org-edit-src-content-indentation 0
        org-export-in-background nil
        org-fontify-quote-and-verse-blocks t
        org-fontify-whole-heading-line t
        org-fontify-done-headline t
        org-fold-catch-invisible-edits 'smart)

  (setq org-ellipsis " â–¾ "
        org-hide-leading-stars t
        org-priority-highest ?A
        org-priority-lowest ?E
        org-priority-faces
        '((?A . 'all-the-icons-red)
          (?B . 'all-the-icons-orange)
          (?C . 'all-the-icons-yellow)
          (?D . 'all-the-icons-green)
          (?E . 'all-the-icons-blue)))

  (setq org-todo-keywords
        '((sequence "TODO" "WORK" "|" "DONE")))

  (setq org-list-demote-modify-bullet
        '(("+" . "-") ("-" . "+") ("*" . "+") ("1." . "a.")))

  (setq org-tag-alist '(("@å·¥ä½œ" . ?w) ("@ç”Ÿæ´»" . ?l) ("@å­¦ä¹ " . ?s)))

  ;; Coding system for HTML export.
  (setq org-html-coding-system 'utf-8)
  (setq org-html-doctype "html5")
  ;; (setq org-html-head
  ;;       "<link rel='stylesheet' type='text/css' href='https://gongzhitaao.org/orgcss/org.css'/> ")

  (setq org-html-head
        "<link rel='stylesheet' type='text/css' href='https://www.labri.fr/perso/nrougier/GTD/GTD.css'/>")

  (after! org-superstar
    ;; other symbols like: ğŸ¦„  ğŸ» "ğŸ™˜" "ğŸ™™" "ğŸ™š" "ğŸ™›" "â˜¯" "â˜·" "â˜²" "â˜µ"
    (setq org-superstar-headline-bullets-list '("ğŸ™˜" "ğŸ™™" "ğŸ™š" "ğŸ™›")
          org-superstar-special-todo-items t
          org-superstar-item-bullet-alist '((?- . ?â€¢) (?* . ?â€“) (?+ . ?â—¦))
          org-superstar-prettify-item-bullets t ))

  (add-hook! 'org-babel-after-execute-hook 'org-display-inline-images 'append)
  (add-hook! 'org-babel-after-execute-hook 'org-toggle-latex-fragment 'append)
  (add-hook! 'org-mode-hook #'+org-pretty-mode))


  ;; ä»»ä½•.org åç¼€çš„æ–‡ä»¶, å‡ä»¥orgmode æ‰“å¼€
  (setq auto-mode-alist (append '(("\\.org$" . org-mode)) auto-mode-alist))
#+end_src
**** org agenda
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(after! org
  ;; FIXME
  (setq org-agenda-files (directory-files-recursively (concat org-directory "roam") "\\.org$"))
  (setq org-agenda-diary-file (concat org-directory "private/standard-diary"))
  (setq diary-file (concat org-directory "private/standard-diary"))

  (setq org-agenda-deadline-faces
        '((1.001 . error)
          (1.0 . org-warning)
          (0.5 . org-upcoming-deadline)
          (0.0 . org-upcoming-distant-deadline)))

  (setq org-agenda-prefix-format '((agenda . "%t %s ")
                                   (todo   . " ")))

  (setq org-agenda-clockreport-parameter-plist
        '(:link t :maxlevel 6 :fileskip0 t :compact t :narrow 60 :score 0))

  (setq org-agenda-hide-tags-regexp ".")

  (setq org-agenda-span 7
        org-agenda-start-on-weekday 1
        org-agenda-log-mode-items '(clock)
        org-agenda-include-all-todo t
        org-agenda-time-leading-zero t
        org-agenda-use-time-grid nil
        calendar-holidays nil
        org-agenda-include-diary t))

#+end_src
**** org babel languages
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(after! org
  (add-to-list 'org-src-lang-modes '("plantuml" . plantuml))
  (setq org-plantuml-jar-path (expand-file-name "~/Documents/emacs/org/private/plantuml.jar"))
  (setq plantuml-default-exec-mode 'jar)
  (setq org-hide-block-startup t)
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((emacs-lisp . t)
     (shell . t)
     (plantuml . t)
     (gnuplot . t))))

#+end_src
**** ç¾åŒ–ç¬¦å·
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(setq-default prettify-symbols-alist '(("#+title:" . "âœ")
                                       ("#+author:" . "ğŸ‘¨")
                                       ("#+caption:" . "â˜°")
                                       ("#+results:" . "ğŸ")
                                       ("#+attr_latex:" . "ğŸ„")
                                       ("#+attr_org:" . "ğŸ””")
                                       ("#+date:" . "âš“")
                                       ("#+property:" . "â˜¸")
                                       (":PROPERTIES:" . "âš™")
                                       (":END:" . ".")
                                       ("#+options:" . "âŒ¥")
                                       ("\\pagebreak" . 128204)
                                       ("#+begin_quote" . "â®")
                                       ("#+end_quote" . "â¯")
                                       ("#+BEGIN_Highlight" . "ğŸ“–")
                                       ("#+END_Highlight" . "ğŸ“œ")
                                       ("#+begin_src" . "â©")
                                       ("#+end_src" . "âª")))


(add-hook! 'org-mode-hook 'prettify-symbols-mode)

(defun org-mode-remove-stars ()
  ;; å…³æ‰ Headline å‰é¢çš„ * ç¬¦å·æ˜¾ç¤º
  (font-lock-add-keywords
   nil
   '(("^\\*+ "
      (0
       (prog1 nil
         (put-text-property (match-beginning 0) (match-end 0)
                            'invisible t)))))))

(add-hook! 'org-mode-hook #'org-mode-remove-stars)
#+end_src
**** custom face
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(after! org
  (custom-set-faces!
    '(outline-1 :weight extra-bold :height 1.25)
    '(outline-2 :weight bold :height 1.15)
    '(outline-3 :weight bold :height 1.12)
    '(outline-4 :weight semi-bold :height 1.09)
    '(outline-5 :weight semi-bold :height 1.06)
    '(outline-6 :weight semi-bold :height 1.03)
    '(outline-8 :weight semi-bold)
    '(outline-9 :weight semi-bold))

  (custom-set-faces
   '(region ((t (:foreground "green" :background "#464646")))))

  (custom-set-faces!
    '(org-document-title :height 1.2)))

;; å…³é—­indent
(after! org
  (custom-set-variables
   '(org-startup-indented nil)))

(after! org
  ;; @Eli å¸®å¿™å†™çš„è§£å†³æ ‡è®°ç¬¦å·å‰åç©ºæ ¼é—®é¢˜çš„ä»£ç , æ„Ÿè°¢.
  (setq org-emphasis-regexp-components '("-[:space:]('\"{[:nonascii:]"
                                         "-[:space:].,:!?;'\")}\\[[:nonascii:]"
                                         "[:space:]"
                                         "."
                                         1))
  (setq org-match-substring-regexp
        (concat
         ;; é™åˆ¶ä¸Šæ ‡å’Œä¸‹æ ‡çš„åŒ¹é…èŒƒå›´ï¼Œorg ä¸­å¯¹å…¶çš„ä»‹ç»è§ï¼š(org) Subscripts and superscripts
         "\\([0-9a-zA-ZÎ±-Î³Î‘-Î©]\\)\\([_^]\\)\\("
         "\\(?:" (org-create-multibrace-regexp "{" "}" org-match-sexp-depth) "\\)"
         "\\|"
         "\\(?:" (org-create-multibrace-regexp "(" ")" org-match-sexp-depth) "\\)"
         "\\|"
         "\\(?:\\*\\|[+-]?[[:alnum:].,\\]*[[:alnum:]]\\)\\)"))
  (org-set-emph-re 'org-emphasis-regexp-components org-emphasis-regexp-components)
  (org-element-update-syntax)
  )
#+end_src
**** org è‡ªå®šä¹‰å‡½æ•°
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(after! org
  ;; æ ‡è®°å­—ç¬¦å‰åç©ºæ ¼ä¼˜åŒ–é—®é¢˜
  (defun eli/org-do-emphasis-faces (limit)
    "Run through the buffer and emphasize strings."
    (let ((quick-re (format "\\([%s]\\|^\\)\\([~=*/_+]\\)"
                            (car org-emphasis-regexp-components))))
      (catch :exit
        (while (re-search-forward quick-re limit t)
          (let* ((marker (match-string 2))
                 (verbatim? (member marker '("~" "="))))
            (when (save-excursion
                    (goto-char (match-beginning 0))
                    (and
                     ;; Do not match if preceded by org-emphasis
                     (not (save-excursion
                            (forward-char 1)
                            (get-pos-property (point) 'org-emphasis)))
                     ;; Do not match in latex fragments.
                     (not (org-inside-LaTeX-fragment-p))
                     ;; Do not match in Drawer.
                     (not (org-match-line
                           "^[    ]*:\\(\\(?:\\w\\|[-_]\\)+\\):[      ]*"))
                     ;; Do not match table hlines.
                     (not (and (equal marker "+")
                               (org-match-line
                                "[ \t]*\\(|[-+]+|?\\|\\+[-+]+\\+\\)[ \t]*$")))
                     ;; Do not match headline stars.  Do not consider
                     ;; stars of a headline as closing marker for bold
                     ;; markup either.
                     (not (and (equal marker "*")
                               (save-excursion
                                 (forward-char)
                                 (skip-chars-backward "*")
                                 (looking-at-p org-outline-regexp-bol))))
                     ;; Match full emphasis markup regexp.
                     (looking-at (if verbatim? org-verbatim-re org-emph-re))
                     ;; Do not span over paragraph boundaries.
                     (not (string-match-p org-element-paragraph-separate
                                          (match-string 2)))
                     ;; Do not span over cells in table rows.
                     (not (and (save-match-data (org-match-line "[ \t]*|"))
                               (string-match-p "|" (match-string 4))))))
              (pcase-let ((`(,_ ,face ,_) (assoc marker org-emphasis-alist))
                          (m (if org-hide-emphasis-markers 4 2)))
                (font-lock-prepend-text-property
                 (match-beginning m) (match-end m) 'face face)
                (when verbatim?
                  (org-remove-flyspell-overlays-in
                   (match-beginning 0) (match-end 0))
                  (when (and (org-fold-core-folding-spec-p 'org-link)
                             (org-fold-core-folding-spec-p 'org-link-description))
                    (org-fold-region (match-beginning 0) (match-end 0) nil 'org-link)
                    (org-fold-region (match-beginning 0) (match-end 0) nil 'org-link-description))
                  (remove-text-properties (match-beginning 2) (match-end 2)
                                          '(display t invisible t intangible t)))
                (add-text-properties (match-beginning 2) (match-end 2)
                                     '(font-lock-multiline t org-emphasis t))
                (when (and org-hide-emphasis-markers
                           (not (org-at-comment-p)))
                  (add-text-properties (match-end 4) (match-beginning 5)
                                       '(invisible t))
                  (add-text-properties (match-beginning 3) (match-end 3)
                                       '(invisible t)))
                (throw :exit t))))))))

  (advice-add #'org-do-emphasis-faces :override #'eli/org-do-emphasis-faces)

  (defun eli/org-element--parse-generic-emphasis (mark type)
  "Parse emphasis object at point, if any.

MARK is the delimiter string used.  TYPE is a symbol among
`bold', `code', `italic', `strike-through', `underline', and
`verbatim'.

Assume point is at first MARK."
  (save-excursion
    (let ((origin (point)))
      (unless (bolp) (forward-char -1))
      (let ((opening-re
             (rx-to-string
              `(seq (or line-start (any space ?- ?\( ?' ?\" ?\{ nonascii))
                ,mark
                (not space)))))
        (when (looking-at opening-re)
          (goto-char (1+ origin))
          (let ((closing-re
                 (rx-to-string
                  `(seq
                    (not space)
                    (group ,mark)
                    (or (any space ?- ?. ?, ?\; ?: ?! ?? ?' ?\" ?\) ?\} ?\\ ?\[
                             nonascii)
                        line-end)))))
            (when (re-search-forward closing-re nil t)
              (let ((closing (match-end 1)))
                (goto-char closing)
                (let* ((post-blank (skip-chars-forward " \t"))
                       (contents-begin (1+ origin))
                       (contents-end (1- closing)))
                  (list type
                        (append
                         (list :begin origin
                               :end (point)
                               :post-blank post-blank)
                         (if (memq type '(code verbatim))
                             (list :value
                                   (and (memq type '(code verbatim))
                                        (buffer-substring
                                         contents-begin contents-end)))
                           (list :contents-begin contents-begin
                                 :contents-end contents-end)))))))))))))
(advice-add #'org-element--parse-generic-emphasis :override #'eli/org-element--parse-generic-emphasis)

  )
#+end_src
*** é…ç½®è‡ªå¸¦åŒ…
**** evil
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(after! evil
  (setq evil-ex-substitute-global t     ; I like my s/../.. to by global by default
        evil-move-cursor-back nil       ; Don't move the block cursor when toggling insert mode
        evil-kill-on-visual-paste nil))
#+end_src
**** ivy
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(after! ivy
  ;; Causes open buffers and recentf to be combined in ivy-switch-buffer
  (setq ivy-use-virtual-buffers t
        +ivy-project-search-engines '(rg)
        ivy-re-builders-alist '((swiper . ivy--regex-plus) (t . ivy--regex-fuzzy))
        counsel-find-file-at-point t
        ivy-wrap nil
        ivy-posframe-display-functions-alist '((t . ivy-posframe-display-at-frame-top-center))
        ivy-posframe-height-alist '((t . 20))
        ivy-posframe-parameters '((internal-border-width . 1))
        ivy-posframe-width 100))
#+end_src
**** doom-modeline
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(use-package! doom-modeline
  :custom
  (doom-modeline-enable-word-count t)
  (doom-modeline-height 10)
  (doom-modeline-continuous-word-count-modes '(markdown-mode gfm-mode org-mode text-mode)))

#+end_src
**** elfeed
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(after! elfeed
  (setq rmh-elfeed-org-files (list (concat org-roam-directory "elfeed.org")))
  (add-hook 'elfeed-search-mode-hook #'elfeed-update)
  (setq elfeed-search-title-max-width 80    ; Maximum titles width
        elfeed-search-title-min-width 40    ; Minimum titles width
        elfeed-search-trailing-width 24     ; Space reserved for feed & tag
        elfeed-search-filter                ; Default filter
        "@1-weeks-ago +unread")

  (defvar elfeed-search-sort-function)
  (defun eli/elfeed-search-filter-source (entry)
    "Filter elfeed search buffer by the feed under cursor."
    (interactive (list (elfeed-search-selected :ignore-region)))
    (when (elfeed-entry-p entry)
      (let ((elfeed-search-sort-function nil))
        (elfeed-search-set-filter
         (concat
          "="
          (replace-regexp-in-string
           (rx "?" (* not-newline) eos)
           ""
           (elfeed-feed-url (elfeed-entry-feed entry))))))))
  )
#+end_src
**** org-roam
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(after! org-roam
  :config
  (org-roam-db-autosync-mode)

  (setq org-roam-directory (concat org-directory "roam/")
        org-roam-db-location (concat org-directory "roam/org-roam.db")
        +org-roam-open-buffer-on-find-file nil)

  (setq org-roam-capture-templates
        `(("n" "default note" plain "%?"
           :if-new
           (file+head "${slug}.org"
                      "#+title: ${title}\n\n ")
           :unnarrowed t))))

#+end_src
**** plantuml
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(after! plantuml-mode
  (setq plantuml-jar-path (expand-file-name "~/Documents/emacs/org/private/plantuml.jar"))
  (setq plantuml-default-exec-mode 'jar))

#+end_src
*** é…ç½®åŠ è£…åŒ…
**** ace-pinyin
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(use-package! ace-pinyin
  :config
  (ace-pinyin-global-mode +1))

#+end_src
**** blink-search
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(add-to-list 'load-path "~/Documents/emacs/local-packages/blink-search")

(require 'blink-search)
(global-set-key (kbd "C-s") 'blink-search)

#+end_src
**** gptel
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(use-package! gptel
   :config
   (setq! gptel-model "gpt-4")
   (setq! gptel-api-key "your-api-key")
   ;; (setq! gptel-model "gpt-3.5-turbo-0613")
   ;; (setq! gptel-api-key "your-api-key")
   )

(global-set-key (kbd "s-l") 'gptel-send)


(defvar eli/gptel-quick-prompts-directory org-roam-directory)

(defun eli/gptel-quick-query (beg end)
  (interactive "r")
  (let* ((prompt (string-split (buffer-substring-no-properties beg end)
                               ":" t " "))
         (gptel--system-message
          (with-temp-buffer
            (insert-file-contents
             (file-name-concat eli/gptel-quick-prompts-directory
                               (file-name-with-extension
                                (car prompt) ".org")))
            (goto-char (point-min))
            (org-next-visible-heading 1)
            (let* ((elt (org-element-at-point))
                   (beg (org-element-property :contents-begin elt))
                   (end (org-element-property :contents-end elt)))
              (buffer-substring-no-properties beg end))))
         (user-prompt (cadr prompt)))
    (gptel-request user-prompt :stream t)
    (deactivate-mark)))

(defun eli/gptel-quick-select-prompt ()
  "Select a prompt."
  (interactive)
  (let* ((full-path (read-file-name "Select: " eli/gptel-quick-prompts-directory))
         (filename (file-name-base full-path)))
    (insert filename)))

(global-set-key (kbd "s-d") 'eli/gptel-quick-select-prompt)
(global-set-key (kbd "s-p") 'eli/gptel-quick-query)

#+end_src
**** focus
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(use-package! focus
  :after org-roam
  :config
  (add-to-list 'focus-mode-to-thing '(org-mode . paragraph)))
#+end_src

**** literate-calc
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(use-package! literate-calc-mode)
#+end_src

**** olivetti
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;;
(use-package! olivetti
  :hook (org-mode . olivetti-mode)
  :config
  ;; è®¾ç½®æ–‡æœ¬å®½åº¦ï¼Œé»˜è®¤æ˜¯ 80 åˆ—
  (setq olivetti-body-width 80))
;;
#+end_src
**** org-appear
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;;
(use-package! org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t
        org-appear-autosubmarkers t
        org-appear-autolinks nil))
;;
#+end_src
**** org-download
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(use-package! org-download
  :defer nil
  :custom
  (org-download-image-dir "~/Documents/emacs/org/roam/images")
  (org-image-actual-width '(400))
  (org-download-heading-lvl nil)
  (org-download-timestamp "")
  :config
  (require 'org-download))

(after! org-download
  (setq org-download-method 'directory)
  (setq org-download-link-format "[[file:images/%s]]\n"))

#+end_src
**** org-roam-ui
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(use-package! org-roam-ui)

#+end_src
**** org-transclusion
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(use-package! org-transclusion
  :after org
  :init
  (map!
   :leader
   :prefix "n"
   :desc "Org Transclusion Mode" "t" #'org-transclusion-mode))

(global-set-key (kbd "C-s-j") 'org-transclusion-add)
#+end_src
**** pangu-spacing
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(use-package! pangu-spacing
  :config
  (global-pangu-spacing-mode 1)
  ;; åœ¨ä¸­è‹±æ–‡ç¬¦å·ä¹‹é—´, çœŸæ­£åœ°æ’å…¥ç©ºæ ¼
  (setq pangu-spacing-real-insert-separtor t))

#+end_src
**** rime
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(use-package! rime
  :custom
  (default-input-method "rime")
  ;; FIXME
  (rime-librime-root "~/Documents/emacs/depend/librime/dist")
  (rime-emacs-module-header-root "/opt/homebrew/Cellar/emacs-plus@29/29.1/include")
  ;; (rime-emacs-module-header-root "/usr/local/opt/emacs-mac/include")
  :config
  (define-key rime-mode-map (kbd "C-i") 'rime-force-enable)
  (setq rime-disable-predicates
        '(rime-predicate-evil-mode-p
          rime-predicate-after-alphabet-char-p
          rime-predicate-current-input-punctuation-p
          rime-predicate-current-uppercase-letter-p
          rime-predicate-punctuation-line-begin-p))
  ;; FIXME
  (setq rime-user-data-dir "~/Library/Rime"))
#+end_src
**** telega
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(use-package! telega
  :config
  (setq telega-chat-show-avatars nil)
  (setq telega-avatar-text-compose-chars nil)
  (setq telega-sticker-size '(0 . 0))
  (setq telega-chat-prompt-format "ğŸ´>: ")
  (setq telega-chat-input-markups '("org"))
  (setq telega-use-images t))

(with-eval-after-load 'telega
  (define-key telega-msg-button-map "k" nil))


#+end_src
**** valign
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(use-package! valign
  :config
  (setq valign-fancy-bar t)
  (add-hook 'org-mode-hook #'valign-mode))


#+end_src
*** é…ç½®è‡ªå®šä¹‰å‡½æ•°
**** ä»·å€¼æŠ•èµ„ DCF å‡½æ•°
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(defun dcf (cf0 g r n)
  "calculate dcf value

CF0ä¸ºåˆå§‹å‡€åˆ©æ¶¦ï¼Œ gä¸ºé¢„è®¡å‡€åˆ©æ¶¦å¹´å¤åˆå¢é•¿ç‡

r ä¸ºæœªæ¥ç°é‡‘æµæŠ˜ç°ç‡ï¼Œ nä¸ºä¼ä¸šæœªæ¥å­˜æ´»å¹´æ•°"
  (interactive "nCF0: \nng: \nnr: \nnn: ")
  (let* ((g1 (expt (1+ g) n))
         (r1  (expt (1+ r) (- n)))
         (cf1 (* cf0 g1 r1)))
    (/ (- cf0 cf1) (- r g))))

#+end_src
**** ä»Šå¹´çš„æ—¶é—´è¿›åº¦å‡½æ•°
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

;; æ’å…¥ä»Šå¹´çš„æ—¶é—´è¿›åº¦æ¡
(defun make-progress (width percent has-number?)
  (let* ((done (/ percent 100.0))
         (done-width (floor (* width done))))
    (concat
     "["
     (make-string done-width ?/)
     (make-string (- width done-width) ? )
     "]"
     (if has-number? (concat " " (number-to-string percent) "%")))))



(defun insert-day-progress ()
  (interactive)
  (let* ((today (time-to-day-in-year (current-time)))
         (percent (floor (* 100 (/ today 365.0)))))
    (insert (make-progress 30 percent t))
    ))

;; SPC i p to insert day progress
(map! :leader :desc "Insert day progress" "i p" #'insert-day-progress)

#+end_src
**** å¿«é€Ÿæ’å…¥æˆªå›¾åˆ°æ–‡ä»¶
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(defun org-insert-image ()
  "insert a image from clipboard"
  (interactive)
  (let* ((path (concat default-directory "images/"))
         (fname (read-string "Enter file name: "))
         (image-file (concat path fname)))
    (if (not (file-exists-p path))
        (mkdir path))
    (do-applescript (concat
                     "set the_path to \"" image-file "\" \n"
                     "set png_data to the clipboard as Â«class PNGfÂ» \n"
                     "set the_file to open for access (POSIX file the_path as string) with write permission \n"
                     "write png_data to the_file \n"
                     "close access the_file"))
    ;; (shell-command (concat "pngpaste " image-file))
    (org-insert-link nil
                     (concat "file:" image-file)
                     "")
    (message image-file))
  (org-display-inline-images)
  )

(map! :leader :desc "Insert image" "i i" #'org-insert-image)

#+end_src
**** æ›´æ–¹ä¾¿çš„æ’å…¥é“¾æ¥
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

;; Learn from: https://xenodium.com/emacs-dwim-do-what-i-mean/
(defun ar/org-insert-link-dwim ()
  "Like `org-insert-link' but with personal dwim preferences."
  (interactive)
  (let* ((point-in-link (org-in-regexp org-link-any-re 1))
         (clipboard-url (when (string-match-p "^http" (current-kill 0))
                          (current-kill 0)))
         (region-content (when (region-active-p)
                           (buffer-substring-no-properties (region-beginning)
                                                           (region-end)))))
    (cond ((and region-content clipboard-url (not point-in-link))
           (delete-region (region-beginning) (region-end))
           (insert (org-make-link-string clipboard-url region-content)))
          ((and clipboard-url (not point-in-link))
           (insert (org-make-link-string
                    clipboard-url
                    (read-string "title: "
                                 (with-current-buffer (url-retrieve-synchronously clipboard-url)
                                   (dom-text (car
                                              (dom-by-tag (libxml-parse-html-region
                                                           (point-min)
                                                           (point-max))
                                                          'title))))))))
          (t
           (call-interactively 'org-insert-link)))))

(map! :leader "i l" #'ar/org-insert-link-dwim)


#+end_src
**** å¿«é€Ÿè°ƒæ•´ Frame å±…ä¸­
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;; Author: @Nasy å­¦å§
;; Description: å°†å½“å‰çª—å£è®¾ç½®ä¸ºå±…ä¸­.

(defun nasy/frame-recenter (&optional frame)
  "Center FRAME on the screen.
FRAME can be a frame name, a terminal name, or a frame.
If FRAME is omitted or nil, use currently selected frame."
  (interactive)
  (unless (eq 'maximised (frame-parameter nil 'fullscreen))
    (let* ((frame (or (and (boundp 'frame) frame) (selected-frame)))
           (frame-w (frame-pixel-width frame))
           (frame-h (frame-pixel-height frame))
            ;; frame-monitor-workarea returns (x y width height) for the monitor
           (monitor-w (nth 2 (frame-monitor-workarea frame)))
           (monitor-h (nth 3 (frame-monitor-workarea frame)))
           (center (list (/ (- monitor-w frame-w) 2)
                         (/ (- monitor-h frame-h) 2))))
      (apply 'set-frame-position (flatten-list (list frame center))))))
;;

#+end_src
**** å¤åˆ¶ org æ–‡å­—åˆ°å…¶å®ƒåœ°æ–¹æ—¶è‡ªåŠ¨ Unfill
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;;
;; Author: @Eli
;; Description: å°† Orgmode æ–‡æœ¬å¤åˆ¶åˆ°å…¶å®ƒè½¯ä»¶æ—¶, å»é™¤ç¡¬æ¢è¡Œå’Œæ ‡è®°ç¬¦å·
;; ä½¿ç”¨æ—¶, C-u + å¤åˆ¶ å³å¯
(require 'ox-ascii)

(defun eli/org2plaintxt (string)
  (cl-flet ((drop-markup (_ content _) (identity content)))
    (cl-letf (((symbol-function #'org-ascii-bold) #'drop-markup)
              ((symbol-function #'org-ascii-italic) #'drop-markup)
              ((symbol-function #'org-ascii-strike-through) #'drop-markup)
              ((symbol-function #'org-ascii-underline) #'drop-markup))
      (let ((org-ascii-text-width 999999)
   (org-ascii-bullets nil)
            (org-ascii-underline nil)
            (org-ascii-verbatim-format "%s"))
        (org-export-string-as string 'ascii t)))))

(defun eli/unfill-string (string)
  (if current-prefix-arg
   (thread-last string eli/org2plaintxt)
    string))

(advice-add #'buffer-substring--filter :filter-return #'eli/unfill-string)
;;

#+end_src
**** æ’å…¥æ°´å¹³åˆ†éš”çº¿
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(defun insert-bold-hline ()
  "Insert a bold horizontal line in Org-mode."
  (interactive)
  (insert (make-string 18 ?â”))
  (insert "\n"))

(global-set-key (kbd "s-h") 'insert-bold-hline)


#+end_src
**** å¿«é€Ÿæ’å…¥ç¬¦å· â–
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(defun insert-custom-symbol ()
  "Inserts the custom symbol 'â–' at the cursor position."
  (interactive)
  (insert "â–"))

(global-set-key (kbd "C-s-i") 'insert-custom-symbol)
#+end_src
**** ä¿®å¤ cl åŒ…çš„å¯åŠ¨æ—¶æŠ¥é”™ä¿¡æ¯

#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
;; å¯åŠ¨æ—¶ *Messages* æœ‰ warning: "Package cl is deprecated"

;; - é“¾æ¥: [[https://github.com/doomemacs/doomemacs/issues/3372][Doom emacs issue #3372]]

;; - åŸå› :
;;   + Emacs 27 ä¹‹åçš„ç‰ˆæœ¬åºŸå¼ƒäº†å†…ç½®åº“=cl=, æ›¿ä¹‹ä»¥=cl-lib=
;;   + ä½†æœ‰å¾ˆå¤šç¬¬ä¸‰æ–¹åŒ…ä»åœ¨ä½¿ç”¨=cl=, æ‰€ä»¥å¯åŠ¨æ—¶ä¼šæœ‰è¯¥æç¤ºã€‚

;; - è§£æ³•: *æ— è§£*
;;   - æ ¹æœ¬è§£: åªèƒ½ç­‰å¾…ç¬¬ä¸‰æ–¹åŒ…å‡çº§å®Œæˆæ›¿æ¢ä¿®å¤

;;   - hack way: åœ¨å‘å‡ºæŠ¥è­¦çš„å‡½æ•°ä¸Šåšæ–‡ç« , å¢åŠ =advice=

(defadvice! fixed-do-after-load-evaluation (abs-file)
  :override #'do-after-load-evaluation
  (dolist (a-l-element after-load-alist)
    (when (and (stringp (car a-l-element))
               (string-match-p (car a-l-element) abs-file))
      (mapc #'funcall (cdr a-l-element))))
  (run-hook-with-args 'after-load-functions abs-file))
#+end_src

*** é…ç½®å¿«æ·é”®
**** é€šç”¨å¿«æ·é”®
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"
(setq doom-localleader-key ",")

(map!
 ;; ä¼—å¦™ä¹‹é—¨, å€¼å¾—åˆ†é…ä¸€ä¸ª SPC SPC
 :leader :desc "All in M-x" "SPC" #'counsel-M-x

 "C-h h" 'helpful-at-point
 "C-h f" 'helpful-function
 "C-h v" 'helpful-variable
 "C-h k" 'helpful-key)


(map! :leader
      :desc "open export dispatch" "e" #'org-export-dispatch)

(map! :leader
      (:prefix "o" :desc "open applications"
               "e" #'elfeed
               "t" #'telega))

(setq mac-command-modifier 'super)
(setq mac-option-modifier 'meta)

(global-set-key (kbd "s-j") 'avy-goto-word-1)
(global-set-key (kbd "s-o") 'org-roam-node-find)
(global-set-key (kbd "s-i") 'org-roam-node-insert)
#+end_src
**** org-roam å¿«æ·é”®
#+begin_src emacs-lisp :tangle "~/.doom.d/config.el"

(map! :leader
      :prefix ("r" . "org-roam")
      "f" #'org-roam-find-file
      ;; "s" #'org-roam-server-mode
      "i" #'org-roam-insert
      "t" #'org-roam-dailies-goto-today)
#+end_src
